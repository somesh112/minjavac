PARSER_BEGIN(Parser)
package parser;

import util.NamePool;
import analysis.syntaxtree.*;

public class Parser {
}
PARSER_END(Parser)


SKIP : /* White space */
{
  " " | "\t" | "\n" | "\r" | "\f"
}


SPECIAL_TOKEN : /* Comments */
{
  < LINE_COMMENT      :
    "//" (~["\n","\r"])* ("\n" | "\r" (~[]))?
  > |

  < FORMAL_COMMENT    :
    "/**" (~["/"] ( ~["*"] | "*" ~["/"] )* ("*")? )? "*/"
  > |

  < MULTILINE_COMMENT :
    "/*" (~["*"] | "*" ~["/"])* ("*")? "*/"
  >
}


TOKEN : /* Keywords */
{
   < INT       : "int" >
 | < VOID      : "void" >
 | < STRING    : "String" >
 | < BOOLEAN   : "boolean" >

 | < NULL      : "null" >
 | < THIS      : "this" >
 | < TRUE      : "true" >
 | < FALSE     : "false" >

 | < MAIN      : "main" >
 | < LENGTH    : "length" >
 | < PRINT_INT    : "System.out.println" >
 | < PRINT_STRING : "System.out.print" >

 | < IF        : "if" >
 | < NEW       : "new" >
 | < ELSE      : "else" >
 | < CLASS     : "class" >
 | < WHILE     : "while" >
 | < PUBLIC    : "public" >
 | < RETURN    : "return" >
 | < STATIC    : "static" >
 | < EXTENDS   : "extends" >
 | < INTERFACE : "interface" >
}


TOKEN : /* Operators */
{
   < ADD  : "+" >
 | < SUB  : "-" >
 | < MULT : "*" >

 | < AND  : "&&" >
 | < NOT  : "!" >
 | < LESS : "<" >

 | < ASSIGN : "=" >
}


TOKEN : /* Separators */
{
   < LPARENS  : "(" >
 | < RPARENS  : ")" >
 | < LBRACKET : "[" >
 | < RBRACKET : "]" >
 | < LBRACE   : "{" >
 | < RBRACE   : "}" >

 | < COMMA : "," >
 | < DOT   : "." >
 | < SEMI  : ";" >
}


TOKEN : /* Numbers */
{
  < NUM : "0" | ["1"-"9"](["0"-"9"])* >
} 


TOKEN : /* Identifiers */
{
    < ID : <LETTER> (<LETTER> | ["0"-"9"])* >

  | < #LETTER : "$" | "_" | ["a"-"z","A"-"Z"] >
}


TOKEN : /* Strings */
{
  < STRING_LITERAL :
      "\""
      (
          ~["\"","\\","\n","\r"]
        | "\\" ["n","t","b","r","f","\\","'","\""]
      )*
      "\""
  >
}


Program Goal() :
{
  MainClass m;
  ClassDeclList cl;
}
{
  m = MainClass() cl = ClassDeclarationList() <EOF>
 { return new Program(m, cl); }
}


ClassDeclList ClassDeclarationList() :
{
  ClassDeclList l = new ClassDeclList();
  ClassDecl cd;
}
{
  (cd = ClassDeclaration() { l.addElement(cd); })*
  { return l; }
}


MainClass MainClass() :
{
  Identifier name, args;
  VarDeclList vl = new VarDeclList();
  StatementList stl = new StatementList();

  Identifier voidId = new Identifier("void@", null);
  vl.addElement(new VarDecl(VoidType.instance(), voidId));
}
{
  <CLASS> name = Identifier()
  <LBRACE>
    <PUBLIC> <STATIC> <VOID> <MAIN>
    <LPARENS>
      <STRING> <LBRACKET> <RBRACKET> args = Identifier()
    <RPARENS>
    <LBRACE>
      [VarDeclarationOrStatement(vl, stl)]
    <RBRACE>
  <RBRACE>
  { return new MainClass(name, args, vl, stl); }
}

ClassDecl ClassDeclaration() :
{
  ClassDecl c;
  Identifier name;
}
{
  <CLASS> name = Identifier() c = ClassDeclarationTail(name)
  { return c; }
}


ClassDecl ClassDeclarationTail(Identifier name) :
{
  Identifier basec;
  VarDeclList vl = new VarDeclList();
  MethodDeclList ml;
}
{
  <EXTENDS> basec = Identifier()
  <LBRACE>
    VarDeclarationList(vl)
    ml = MethodDeclarationList()
  <RBRACE>
  { return new ClassDeclExtends(name, basec, vl, ml); }

  |

  <LBRACE>
    VarDeclarationList(vl)
    ml = MethodDeclarationList()
  <RBRACE>
  { return new ClassDeclSimple(name, vl, ml); }
}


void VarDeclarationList(VarDeclList vl) :
{ }
{
  (VarDeclaration(vl))*
}


MethodDeclList MethodDeclarationList() :
{
  MethodDeclList ml = new MethodDeclList();
  MethodDecl m;
}
{
  (m = MethodDeclaration() { ml.addElement(m); })*
  { return ml; }
}


void VarDeclaration(VarDeclList vl) :
{
  Type t;
  Identifier i;
}
{
  t = Type() i = Identifier() { vl.addElement(new VarDecl(t, i)); }
  (<COMMA>   i = Identifier() { vl.addElement(new VarDecl(t, i)); })*
  <SEMI>
}


MethodDecl MethodDeclaration() :
{
  Type returnT;
  Identifier name;
  FormalList params;
  VarDeclList varL = new VarDeclList();
  StatementList stmtL = new StatementList();
  Exp returnE;

  Identifier voidId = new Identifier("void@", null);
  varL.addElement(new VarDecl(VoidType.instance(), voidId));
}
{
  <PUBLIC> returnT = Type() name = Identifier()
  <LPARENS>
    params = FormalParameterList()
  <RPARENS>
  <LBRACE>
    [VarDeclarationOrStatement(varL, stmtL)]
    <RETURN> returnE = Expression() <SEMI>
  <RBRACE>
  {
    return new MethodDecl(
      returnT, name, params, varL, stmtL, returnE
    );
  }
}


FormalList FormalParameterList() :
{
  FormalList fl = new FormalList();
  Formal f;
}
{
  [
    f = FormalParameter()
    { fl.addElement(f); }
    (<COMMA> f = FormalParameter() { fl.addElement(f); })*
  ]
  { return fl; }
}


Formal FormalParameter() :
{
  Type varT;
  Identifier name;
}
{
  varT = Type() name = Identifier()
  { return new Formal(varT, name); }
}


Type Type() :
{
  Type t;
  Identifier id;
}
{
   t = ArrayOrIntegerType()
   { return t; }
 | t = BooleanType()
   { return t; }
 | id = Identifier()
   { return new IdentifierType(id.toString()); }
}


Type ArrayOrIntegerType() :
{}
{
  <INT>
  [<LBRACKET> <RBRACKET> { return IntArrayType.instance(); }]
  { return IntegerType.instance(); }
}


Type BooleanType() :
{}
{
  <BOOLEAN>
  { return BooleanType.instance(); }
}


void VarDeclarationOrStatement(VarDeclList vl, StatementList stl) :
{
  Statement stmt;
  Identifier id;
  Type t;
}
{
   (
     t = ArrayOrIntegerType() |
     t = BooleanType()
   )
   VarDeclarationTail(t, vl, stl)
 |
   (
     stmt = Block() |
     stmt = IfStatement() |
     stmt = WhileStatement() |
     stmt = PrintIntStatement() |
     stmt = PrintStringStatement() |
     stmt = ThisMethodCallStmt()
   )
   { stl.addElement(stmt); }
   Statements(stl)
 |
   id = Identifier() IdentifierDeclarationOrStatement(id, vl, stl)
}


void VarDeclarationTail(
    Type t, VarDeclList vl, StatementList stl) :
{
  Identifier id;
}
{
  singleVarDecl(t,vl,stl) (<COMMA> singleVarDecl(t,vl,stl))* <SEMI>
  VarDeclarationOrStatement(vl, stl)
}


void singleVarDecl(Type t, VarDeclList vl, StatementList stl) :
{
  Identifier id;
  Exp e;
}
{
  id = Identifier()
  [<ASSIGN> e = Expression() { stl.addElement(new Assign(id, e)); }]
  { vl.addElement(new VarDecl(t, id)); }
}


void IdentifierDeclarationOrStatement(
    Identifier id1, VarDeclList vl, StatementList stl) :
{
  Identifier id2;
  Statement stmt;
  Type t = new IdentifierType(id1.toString());
}
{
   singleVarDecl(t,vl,stl) (<COMMA> singleVarDecl(t,vl,stl))* <SEMI>
   VarDeclarationOrStatement(vl, stl)
 |
   stmt = IdentifierStatementTail(id1)
   { stl.addElement(stmt); }
   Statements(stl)
}


void Statements(StatementList stl) :
{
  Statement stmt;
}
{
  (stmt = Statement() { stl.addElement(stmt); })*
}


Statement Statement() :
{
  Identifier id;
  Statement stmt;
}
{
   (
     stmt = Block() |
     stmt = IfStatement() |
     stmt = WhileStatement() |
     stmt = PrintIntStatement() |
     stmt = PrintStringStatement() |
     stmt = ThisMethodCallStmt()
   )
   { return stmt; }
 |
   id = Identifier() stmt = IdentifierStatementTail(id)
   { return stmt; }
}


Statement Block() :
{
  StatementList stmtL = new StatementList();
}
{
  <LBRACE> Statements(stmtL) <RBRACE>
  { return new Block(stmtL); }
}


Statement IfStatement() :
{
  Exp e;
  Statement s1, s2;
}
{
  <IF> <LPARENS> e = Expression() <RPARENS>
    s1 = Statement()
  <ELSE>
    s2 = Statement()
  { return new If(e, s1, s2); }
}


Statement WhileStatement() :
{
  Exp e;
  Statement stmt;
}
{
  <WHILE> <LPARENS> e = Expression() <RPARENS> stmt = Statement()
  { return new While(e, stmt); }
}


Statement PrintIntStatement() :
{
  Exp e;
}
{
  <PRINT_INT> <LPARENS> e = Expression() <RPARENS> <SEMI>
  { return new Print(e); }
}


Statement PrintStringStatement() :
{
  String s;
}
{
  <PRINT_STRING> <LPARENS> s = StringLiteral() <RPARENS> <SEMI>
  { return new PrintString(s); }
}


Statement IdentifierStatementTail(Identifier id) :
{
  Exp e1, e2;
  ExpList el = new ExpList();
  Identifier voidId;
  Call callExp;
}
{
    <LBRACKET> e1 = Expression() <RBRACKET>
    <ASSIGN> e2 = Expression() <SEMI>
    { return new ArrayAssign(id, e1, e2); }
  |
    <ASSIGN> e1 = Expression() <SEMI>
    { return new Assign(id, e1); }
  |
    { Identifier idM; }
    <DOT> idM = Identifier() <LPARENS> [ExpressionList(el)] <RPARENS> <SEMI>
    {
      voidId = new Identifier("void@", id.getToken());
      callExp = new Call(new Identifier(id), idM, el);

      return new Assign(voidId, callExp);
    }
  |
    <LPARENS> [ExpressionList(el)] <RPARENS> <SEMI>
    {
      voidId = new Identifier("void@", id.getToken());
      callExp = new Call(new This(), id, el);

      return new Assign(voidId, callExp);
    }
}


Statement ThisMethodCallStmt() :
{
  Identifier mId;
  ExpList el = new ExpList();
  Identifier voidId;
  Call callExp;
}
{
  <THIS> <DOT> mId = Identifier()
  <LPARENS> [ExpressionList(el)] <RPARENS>
  <SEMI>
  {
    voidId = new Identifier("void@", mId.getToken());
    callExp = new Call(new This(), mId, el);

    return new Assign(voidId, callExp);
  }
}


Exp Expression() :
{
  Exp e;
}
{
  e = AndExpression()
  { return e; }
}


Exp AndExpression() :
{
  Exp x, y;
}
{
  x = LessThanExpression()
  (
    <AND> y = LessThanExpression()
    { x = new And(x, y); }
  )*
  { return x; }
}


Exp LessThanExpression() :
{
  Exp x, y;
}
{
  x = AdditiveExpression()
  [
    <LESS> y = AdditiveExpression()
    { x = new LessThan(x, y); }
  ]
  { return x; }
}


Exp AdditiveExpression() :
{
  Exp x, y;
}
{
  x = TimesExpression()
  (
      <ADD> y = TimesExpression() { x = new Plus(x, y); }
    |
      <SUB> y = TimesExpression() { x = new Minus(x, y); }
  )*
  { return x; }
}


Exp TimesExpression() :
{
  Exp x, y;
}
{
  x = PrefixExpression()
  (
    <MULT> y = PrefixExpression()
    { x = new Times(x, y); }
  )*
  { return x; }
}


Exp PrefixExpression() :
{
  Exp x;
  int c = 0;
}
{
  (<NOT> { ++c; })*
  x = PostFixExpression()
  { for (int i = 0; i < c; ++i) x = new Not(x); }
  { return x; }
}


Exp PostFixExpression() :
{
  Exp e, pr;
}
{
  pr = PrimaryExpression()
  [
     (  e = ArrayLookup(pr)
      | e = ArrayLengthOrMethodCall(pr)
      | e = ThisMethodCall(pr) )
     { return e; }
  ]
  { return pr; }
}


Exp ArrayLookup(Exp pr) :
{
  Exp e;
}
{
  <LBRACKET> e = Expression() <RBRACKET>
  { return new ArrayLookup(pr, e); }
}


Exp ArrayLengthOrMethodCall(Exp pr) :
{
  Exp e;
}
{
  <DOT> e = ArrayLengthOrMethodCallTail(pr)
  { return e; }
}


Exp ArrayLengthOrMethodCallTail(Exp pr) :
{
  Identifier id;
  ExpList el = new ExpList();
}
{
   <LENGTH>
   { return new ArrayLength(pr); }
 |
   id = Identifier() <LPARENS> [ExpressionList(el)] <RPARENS>
   { return new Call(pr, id, el); }
}


Exp ThisMethodCall(Exp pr) :
{
  ExpList el = new ExpList();
}
{
  <LPARENS> [ExpressionList(el)] <RPARENS>
  {
    if (!(pr instanceof Identifier))
      throw generateParseException();

    Identifier mId = (Identifier)pr;
    return new Call(new This(), mId, el);
 }
}


Exp PrimaryExpression() :
{
  Exp e;
  Identifier id;
}
{
   e = IntegerLiteral()
   { return e; }
 | <TRUE>
   { return new True(); }
 | <FALSE>
   { return new False(); }
 | id = Identifier()
   { return new Identifier(id); }
 | <THIS>
   { return new This(); }
 | <LPARENS> e = Expression() <RPARENS>
   { return e; }
 | <NEW> e = ArrayOrObjectAllocation()
   { return e; }
}


Exp ArrayOrObjectAllocation() :
{
  Identifier id;
  Exp e;
}
{
   <INT> <LBRACKET> e = Expression() <RBRACKET>
   { return new NewArray(e); }
 | id = Identifier() <LPARENS> <RPARENS>
   { return new NewObject(id); }
}


void ExpressionList(ExpList el) :
{
  Exp e;
}
{
  e = Expression()
  { el.addElement(e); }
  (
    <COMMA> e = Expression()
    { el.addElement(e); }
  )*
}


Identifier Identifier() :
{
  Token t;
}
{
  t = <ID> { return new Identifier(t.image, t); }
}

Exp IntegerLiteral() :
{
  Token t;
  int num;
}
{
  t = <NUM>
  {
    num = Integer.valueOf(t.image);
    return new IntegerLiteral(num);
  }
}

String StringLiteral() :
{
  Token t;
}
{
  t = <STRING_LITERAL>
  {
    return t.image.substring(1, t.image.length()-1)
                  .replace("\\n", "\n")
                  .replace("\\t", "\t")
                  .replace("\\b", "\b")
                  .replace("\\r", "\r")
                  .replace("\\f", "\f")
                  .replace("\\\\", "\\")
                  .replace("\\'", "\'")
                  .replace("\\\"", "\"");
  }
}
